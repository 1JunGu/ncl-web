;================================================
;  ndvi_1.ncl
;================================================
; Concepts illustrated:
;   - Read type 'short' and unpack NDVI and EVI
;   - Plot level-3 MYD13C1 data
;=================================================
; MODIS:  NDVI: Normalized Difference Vegetation Index
; https://lpdaac.usgs.gov/dataset_discovery/modis/modis_products_table/myd13c1
;=================================================
; Global MYD13C1 data are cloud-free spatial composites of the 
; gridded 16-day 1-kilometer MYD13A2, and are provided as a level-3 product 
; projected on a 0.05 degree (5,600-meter) geographic Climate Modeling Grid (CMG). 
;
; Cloud-free global coverage is achieved by replacing clouds with the 
; historical MODIS time series climatology record.
;===============================================================
  TEST = False  

  diri = "./"
  fili = "MYD13C1.A2017185.006.2017202045730.hdf"

  pthi = diri+fili
  exti = ".he2"

  f    = addfile(pthi+exti, "r")

  ndvi   = f->CMG_0_05_Deg_16_days_NDVI_MODIS_Grid_16Day_VI_CMG
  ndvi@scale_factor := 1.0/ndvi@scale_factor            ; CF sacle_factor
  ndvi  := short2flt(ndvi)                              ; unpack
  printVarSummary(ndvi)
  printMinMax(ndvi,0)

  evi    = f->CMG_0_05_Deg_16_days_EVI_MODIS_Grid_16Day_VI_CMG
  evi@scale_factor := 1.0/evi@scale_factor             ; CF sacle_factor
  evi   := short2flt(evi)                              ; unpack
  printVarSummary(evi)
  printMinMax(evi,0)

;---Extract info (demo)

;;opt = True
;;opt@PrintStat = True
;;stat_ndvi = stat_dispersion(ndvi, opt )
;;stat_evi  = stat_dispersion(evi , opt )

;---Plot

;        "MYD13C1.A2017185.006.2017202045730.hdf"
;         0123456789012345678901234567890123456789
;                   1         2         3
  yyyyddd_1 = toint(str_get_cols(fili,  9, 15))
  yyyyddd_2 = toint(str_get_cols(fili, 21, 27))
  ymd_1     = yyyyddd_to_yyyymmdd(yyyyddd_1)
  ymd_2     = yyyyddd_to_yyyymmdd(yyyyddd_2)

  wks = gsn_open_wks("png","ndvi_modis")      ; send graphics to PNG file

  res                  = True                ; plot mods desired
  res@gsnMaximize      = True 

  res@cnFillOn         = True                ; color Fill 
  res@cnFillMode       = "RasterFill"        ; Raster Mode
  res@cnLinesOn        = False               ; Turn off contour lines

  res@mpCenterLonF     = 0                   ; set map center at 180
  res@mpFillOn         = False

  res@cnMinLevelValF   =  -0.25              ; set min contour level
  res@cnMaxLevelValF   =   0.95              ; one less than max
  res@cnLevelSpacingF  =   0.05              ; set contour spacing
  res@cnFillPalette    = "WhiteBlueGreenYellowRed"   ; <<< VEGETATION ...place holder

  setvalues NhlGetWorkspaceObjectId() 
    "wsMaximumSize" : 500000000
  end setvalues 

  res@tiMainString      = fili
  res@gsnRightString    = ymd_1+"-"+ymd_2
  plot = gsn_csm_contour_map_ce(wks, ndvi, res) ; create plot
  plot = gsn_csm_contour_map_ce(wks, evi , res) ; create plot
